TITLE:
    Parental mental health and youth antisocial behaviour
    2 SEM using 2-factor theoretical ASB construct (Bayes)

DATA:
    file = "N:\no-backup\Frida og Thea\mi_asb.dat";

VARIABLE:
    names =
        YAGE PAGE SIB INC                           ! Demographics
        scl8_01 scl8_02 scl8_03 scl8_04 scl8_05     ! Parental mental health
        scl8_06 scl8_07 scl8_08                     !   (PMH)
        cbcl002 cbcl026 cbcl028 cbcl039 cbcl043     ! Rule-breaking behaviour
        cbcl063 cbcl067 cbcl072 cbcl073 cbcl081     !   (RBB)
        cbcl082 cbcl090 cbcl096 cbcl099 cbcl101
        cbcl105 cbcl106
        cbcl003 cbcl016 cbcl019 cbcl020 cbcl021     ! Aggression
        cbcl022 cbcl023 cbcl037 cbcl057 cbcl068     !   (AGG)
        cbcl086 cbcl087 cbcl088 cbcl089 cbcl094
        cbcl095 cbcl097 cbcl104
        fes_01 fes_11 fes_21 fes_31 fes_41          ! Family cohesion
        fes_51 fes_61 fes_71 fes_81                 !   (COH)
        fes_03 fes_13 fes_23 fes_33 fes_43          ! Family conflict
        fes_53 fes_63 fes_73 fes_83                 !   (CON)
        PMH_sum                                     ! PMH sum score
        CON_sum                                     ! CON sum score
        COH_sum                                     ! COH sum score
        ASB_sum                                     ! ASB sum score
        SES                                         ! Socioeconomic status
        ;

    usevariables =                                  ! Discard sum scores
        YAGE PAGE SIB INC
        scl8_01 scl8_02 scl8_03 scl8_04 scl8_05
        scl8_06 scl8_07 scl8_08
        cbcl002 cbcl026 cbcl028 cbcl039 cbcl043
        cbcl063 cbcl067 cbcl072 cbcl073 cbcl081
        cbcl082 cbcl090 cbcl096 cbcl099 cbcl101
        cbcl105 cbcl106
        cbcl003 cbcl016 cbcl019 cbcl020 cbcl021
        cbcl022 cbcl023 cbcl037 cbcl057 cbcl068
        cbcl086 cbcl087 cbcl088 cbcl089 cbcl094
        cbcl095 cbcl097 cbcl104
        fes_01 fes_11 fes_21 fes_31 fes_41
        fes_51 fes_61 fes_71 fes_81
        fes_03 fes_13 fes_23 fes_33 fes_43
        fes_53 fes_63 fes_73 fes_83
        SES
        ;

    categorical =                                   ! Likert scale
        scl8_01 scl8_02 scl8_03 scl8_04 scl8_05
        scl8_06 scl8_07 scl8_08
        cbcl002 cbcl026 cbcl028 cbcl039 cbcl043
        cbcl063 cbcl067 cbcl072 cbcl073 cbcl081
        cbcl082 cbcl090 cbcl096 cbcl099 cbcl101
        cbcl105 cbcl106
        cbcl003 cbcl016 cbcl019 cbcl020 cbcl021
        cbcl022 cbcl023 cbcl037 cbcl057 cbcl068
        cbcl086 cbcl087 cbcl088 cbcl089 cbcl094
        cbcl095 cbcl097 cbcl104
        fes_01 fes_11 fes_21 fes_31 fes_41
        fes_51 fes_61 fes_71 fes_81
        fes_03 fes_13 fes_23 fes_33 fes_43
        fes_53 fes_63 fes_73 fes_83
        ;

    missing = all (-99);                            ! -99 denotes missing data

DATA IMPUTATION:
    impute =                                        ! (c) = categorical
        YAGE PAGE SIB INC
        scl8_01 (c) scl8_02 (c) scl8_03 (c) scl8_04 (c) scl8_05 (c)
        scl8_06 (c) scl8_07 (c) scl8_08 (c)
        cbcl002 (c) cbcl026 (c) cbcl028 (c) cbcl039 (c) cbcl043 (c)
        cbcl063 (c) cbcl067 (c) cbcl072 (c) cbcl073 (c) cbcl081 (c)
        cbcl082 (c) cbcl090 (c) cbcl096 (c) cbcl099 (c) cbcl101 (c)
        cbcl105 (c) cbcl106 (c)
        cbcl003 (c) cbcl016 (c) cbcl019 (c) cbcl020 (c) cbcl021 (c)
        cbcl022 (c) cbcl023 (c) cbcl037 (c) cbcl057 (c) cbcl068 (c)
        cbcl086 (c) cbcl087 (c) cbcl088 (c) cbcl089 (c) cbcl094 (c)
        cbcl095 (c) cbcl097 (c) cbcl104 (c)
        fes_01 (c) fes_21 (c) fes_31 (c) fes_41 (c)
        fes_51 (c) fes_61 (c) fes_71 (c) fes_81 (c)
        fes_03 (c) fes_13 (c) fes_23 (c) fes_33 (c) fes_43 (c)
        fes_53 (c) fes_73 (c) fes_83 (c)
        SES (c)
        ;

    ndatasets = 10;                                 ! Order 10 sets of MI data
    save = imp*.dat;                                ! Save imputed datasets

ANALYSIS:
    estimator = bayes;                              ! Bayesian estimation
    algorithm = gibbs (rw);                         ! Gibbs sampler random walk
    processors = 4;                                 ! Use 4 CPU cores
    chains = 4;                                     ! Initiate 4 MCMC chains
    mcseed = 2023;                                  ! Set seed for replication
    thin = 20;                                      ! Draw every 20 iterations

MODEL:
! Measurement part

    ! Input variable (X)

        PMH_lat by                                  ! Parental mental health
            scl8_01 !(l10)                          ! Do not label 1st entry
            scl8_02 (l11)                           !   in BY syntax
            scl8_03 (l12)
            scl8_04 (l13)
            scl8_05 (l14)
            scl8_06 (l15)
            scl8_07 (l16)
            scl8_08 (l17)
            ;
        ! Residual correlation as per suggestion by EFA
            scl8_01 with scl8_02 scl8_03 scl8_06;
            scl8_06 with scl8_07;

    ! Outcome variable (Y)

        RBB_lat by                                  ! Rule-breaking behaviour
            cbcl002 !(l20)
            cbcl026 (l21)
            cbcl028 (l22)
            cbcl039 (l23)
            cbcl043 (l24)
            cbcl063 (l25)
            cbcl067 (l26)
            cbcl072 (l27)
            cbcl073 (l28)
            cbcl081 (l29)
            cbcl082 (l2a)
            cbcl090 (l2b)
            cbcl096 (l2c)
            cbcl099 (l2d)
            cbcl101 (l2e)
            cbcl105 (l2f)
            cbcl106 (l2g)
            ;
        ! Residual correlation as per suggestion by EFA
            cbcl002 with cbcl099;
            cbcl039 with cbcl063;

        AGG_lat by                                  ! Aggression
            cbcl003 !(l30)
            cbcl016 (l31)
            cbcl019 (l32)
            cbcl020 (l33)
            cbcl021 (l34)
            cbcl022 (l35)
            cbcl023 (l36)
            cbcl037 (l37)
            cbcl057 (l38)
            cbcl068 (l39)
            cbcl086 (l3a)
            cbcl087 (l3b)
            cbcl088 (l3c)
            cbcl089 (l3d)
            cbcl094 (l3e)
            cbcl095 (l3f)
            cbcl097 (l3g)
            cbcl104 (l3h)
            ;
        ! Residual correlation as per suggestion by EFA
            cbcl016 with cbcl094;
            cbcl020 with cbcl021;
            cbcl086 with cbcl087;
            cbcl087 with cbcl088;
            cbcl088 with cbcl089;

        ASB_lat by                                  ! Antisocial behavior
            RBB_lat
            AGG_lat
            ;
        RBB_lat with AGG_lat;

    ! Mediators (M)

        COH_lat by                                  ! Family cohesion
            fes_01 !(l40)
            fes_11 (l41)
            fes_21 (l42)
            fes_31 (l43)
            fes_41 (l44)
            fes_51 (l45)
            fes_61 (l46)
            fes_71 (l47)
            fes_81 (l48)
            ;

        CON_lat by                                  ! Family conflict
            fes_03 !(l50)
            fes_13 (l51)
            fes_23 (l52)
            fes_33 (l53)
            fes_43 (l54)
            fes_53 (l55)
            fes_63 (l56)
            fes_73 (l57)
            fes_83 (l58)
            ;
        ! Residual correlation as per suggestion by EFA
            fes_23 with fes_53;
            fes_63 with fes_73;

! Structural part

    CON_lat on PMH_lat (a1);                        ! X -> M1
    COH_lat on PMH_lat (a2);                        ! X -> M2

    ASB_lat on
        CON_lat (b1)                                ! M1 -> Y
        COH_lat (b2)                                ! M2 -> Y
        PMH_lat (d)                                 ! X -> Y
        ;

    ASB_lat on                                      ! Control variables
        YAGE (c1)
        PAGE (c2)
        SIB (c3)
        INC (c4)
        ;

    ASB_lat on SES (e4);                            ! SES affects X, M and Y?
    PMH_lat on SES (e1);
    CON_lat on SES (e2);
    COH_lat on SES (e3);

    CON_lat with COH_lat (f1);                      ! Family conditions covary

MODEL PRIORS:

    ! From Model 1 path analyses
        a1 ~ N(0.386, 0.074**);
        a2 ~ N(-0.297, 0.074**);

        b1 ~ N(0.295, 0.078**);
        b2 ~ N(-0.062, 0.071**);

        d ~ N(0.274, 0.075**);

        c1 ~ N(0.114, 0.077**);
        c2 ~ N(-0.059, 0.082**);
        c3 ~ N(0.092, 0.085**);
        c4 ~ N(-0.011, 0.085**);

        e1 ~ N(-0.135, 0.085**);
        e2 ~ N(0.186, 0.085**);
        e3 ~ N(0.141, 0.060**);
        e4 ~ N(-0.021, 0.084**);

        f1 ~ U(-1, 1);

    ! From EFA (PMH)
    !   l10 ~ N(0.872, 0.026**);                    ! Do not specify 1st entry
        l11 ~ N(0.891, 0.028**);                    !   in BY syntax
        l12 ~ N(0.849, 0.029**);
        l13 ~ N(0.862, 0.030**);
        l14 ~ N(0.825, 0.029**);
        l15 ~ N(0.773, 0.034**);
        l16 ~ N(0.716, 0.043**);
        l17 ~ N(0.515, 0.070**);

    ! From EFA (RBB)
    !   l20 ~ N(0.752, 0.054**);
        l21 ~ N(0.517, 0.069**);
        l22 ~ N(0.750, 0.049**);
        l23 ~ N(0.640, 0.057**);
        l24 ~ N(0.818, 0.043**);
        l25 ~ N(0.467, 0.079**);
        l26 ~ N(0.526, 0.084**);
        l27 ~ N(0.437, 0.103**);
        l28 ~ N(0.774, 0.082**);
        l29 ~ N(0.782, 0.045**);
        l2a ~ N(0.643, 0.062**);
        l2b ~ N(0.505, 0.075**);
        l2c ~ N(0.600, 0.082**);
        l2d ~ N(0.722, 0.059**);
        l2e ~ N(0.452, 0.075**);
        l2f ~ N(0.702, 0.138**);
        l2g ~ N(0.441, 0.090**);

    ! From EFA (AGG)
    !   l30 ~ N(0.874, 0.030**);
        l31 ~ N(0.807, 0.044**);
        l32 ~ N(0.648, 0.061**);
        l33 ~ N(0.744, 0.046**);
        l34 ~ N(0.843, 0.037**);
        l35 ~ N(0.768, 0.042**);
        l36 ~ N(0.473, 0.070**);
        l37 ~ N(0.816, 0.036**);
        l38 ~ N(0.780, 0.042**);
        l39 ~ N(0.764, 0.045**);
        l3a ~ N(0.634, 0.057**);
        l3b ~ N(0.724, 0.050**);
        l3c ~ N(0.759, 0.045**);
        l3d ~ N(0.667, 0.055**);
        l3e ~ N(0.764, 0.052**);
        l3f ~ N(0.803, 0.038**);
        l3g ~ N(0.784, 0.040**);
        l3h ~ N(0.817, 0.041**);

    ! From EFA (COH)
    !   l40 ~ N(0.747, 0.071**);
        l41 ~ N(0.031, 0.116**);
        l42 ~ N(0.328, 0.108**);
        l43 ~ N(0.766, 0.072**);
        l44 ~ N(-0.540, 0.093**);
        l45 ~ N(0.813, 0.064**);
        l46 ~ N(0.806, 0.059**);
        l47 ~ N(0.832, 0.062**);
        l48 ~ N(0.781, 0.063**);

    ! From EFA (CON)
    !   l50 ~ N(0.926, 0.040**);
        l51 ~ N(-0.889, 0.051**);
        l52 ~ N(0.754, 0.071**);
        l53 ~ N(-0.735, 0.068**);
        l54 ~ N(0.698, 0.070**);
        l55 ~ N(0.702, 0.084**);
        l56 ~ N(-0.141, 0.118**);
        l57 ~ N(0.287, 0.143**);
        l58 ~ N(0.720, 0.075**);

MODEL CONSTRAINT:
    new(ind1);                                      ! Indirect path:
    ind1 = a1 * b1;                                 !   PMH -> CON -> ASB

    new(ind2);                                      ! Indirect path
    ind2 = a2 * b2;                                 !   PMH -> COH -> ASB

    new(tot);
    tot = ind1 + ind2 + d;                          ! Total effect

OUTPUT:
    stdyx;                                          ! Standarized output
