TITLE:
    M3: Path analysis for gender pay gap (M2 + demographic variables)

DATA:
    file = implist.dat;
    type = imputation;

VARIABLE:
    ! Data table header
    names =
        log_EARN
        LIT NUM PSL
        MALE cAGE cAGEsq IM1 IM2 cEDU cEDUsq cross
        VARSTRAT VARUNIT SPFWT0
        ;

    usevar =
        log_EARN
        LIT NUM PSL
        MALE cAGE cAGEsq IM1 IM2 cEDU cEDUsq
        VARSTRAT VARUNIT SPFWT0
        ;

    ! Missing data indicator
    missing = all(-99);

    ! Final participant weight
weight = SPFWT0;

    ! Clustering in sampling units
    cluster = VARUNIT;

    ! Stratification
    stratification = VARSTRAT;

ANALYSIS:
    type = complex;
    estimator = MLR;
    h1iterations = 10000;

MODEL:
    log_EARN on
        LIT NUM PSL     ! Skills
        MALE            ! Gender difference (Main interest of current study)
        cAGE cAGEsq     ! Age effect (with age discrimination parabola)
        IM1 IM2         ! Migration penalty/premium?
        cEDU cEDUsq     ! Return on education (with diminishing marginal return)
        ;

    cEDU with
        LIT NUM PSL
        ;

OUTPUT:
!    stdy;       ! Standardised parameter estimates
!    stdyx;      ! Fully standardised parameters
    sampstat;   ! Sample statistics
    cinterval;  ! Confidence intervals
