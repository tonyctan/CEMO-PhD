REM--SYSTAT CODE FILE C11E03SY.SYC READS DATA FILE C1103DT.TXT
REM--CHAPTER 11 EXAMPLE 2 AND tABLE 11.3.1 OF 87 ACADEMIC CASES SOME WITH MISSING DATA
IMPORT "C:\CCWA\CHAP11\DATA\C1103DT.txt" / TYPE=ASCII 

REM--In the latter stages of this program some results of analyses will be saved to the data folder.
REM--First we change the missing values from '999' to .

IF time = 999 then let time = .
IF pub = 999 then let pub = .
IF cit = 999 then let cit = .
IF cita = 999 then let cita = .

NOTE "THIS EXAMPLE FROM 11.3.3 COMPARES THE ALTERNATIVE MISSING DATA METHODS"
NOTE "THE FIRST IS LISTWISE REGRESSION"

REGRESS
MODEL SALARY = CONSTANT+TIME+PUB+CIT+SEX
ESTIMATE

NOTE "THE SECOND USES PLUGGED MEANS"
NOTE "THE MEANS USED VARY SLIGHTLY FROM THOSE USED IN THE BOOK AND RETURN DIFFERENT VALUES."

STATS
STATS TIME PUB CIT /  Mean

REM--Now we 'plug' these means in for the missing values.
LET timemean = time
IF time = . then let timemean = 6.772
LET pubmean = pub
IF pub = . then let pubmean = 17.753
LET citmean = cit
IF cit = . then let citmean = 38.614


REGRESS
MODEL SALARY = CONSTANT+TIMEMEAN+PUBMEAN+CITMEAN+SEX
ESTIMATE

NOTE "THE THIRD USES PLUGGED MEANS AND MISSING DATA DICHOTOMIES"

LET timemiss=0
IF time = . THEN LET timemiss = 1
LET pubmiss = 0
IF pub = . THEN LET pubmiss = 1
LET citmiss = 0
IF cit = . THEN LET citmiss = 1

REGRESS
MODEL SALARY = CONSTANT+TIMEMEAN+PUBMEAN+CITMEAN+SEX+TIMEMISS+PUBMISS+CITMISS
ESTIMATE

STATS
STATS  /  Maximum Mean Minimum SD N
NOTE "THE FOURTH USES A PAIRWISE COVARIANCE MATRIX"
REM -- Following commands were produced by the CORR dialog:
CORR
SAVE 'C:\CCWA\CHAP11\DATA\CORRPAIR.syd'
COVARIANCE TIME PUB SEX CIT SALARY / PAIRWISE EM NORMAL
REM -- End of commands from the CORR dialog
USE 'C:\CCWA\CHAP11\DATA\CORRPAIR.syd'
REM -- Following commands were produced by the REGRESS dialog:
REGRESS
MODEL SALARY = TIME+PUB+SEX+CIT /N=80
ESTIMATE
REM -- End of commands from the REGRESS dialog

IMPORT "C:\CCWA\CHAP11\DATA\C1103DT.txt" / TYPE=ASCII 

NOTE "THE FIFTH METHOD USES ESTIMATED CITATIONS FROM THE PREVIOUS EXAMPLE,
      AND NEWLY GATHERED DATA FOR THE REST"

REGRESS
MODEL SALARY = CONSTANT+TIMEA+PUBA+CITB+SEX

ESTIMATE

NOTE "THE FINAL USES AN EM ALGORITHM ESTIMATE OF THE MISSING DATA"
IMPORT "C:\CCWA\CHAP11\DATA\C1103DT.txt" / TYPE=ASCII 

MISSING
SAVE 'C:\CCWA\CHAP11\DATA\MISS_EM.syd'
MODEL TIME PUB SEX CIT SALARY
ESTIMATE 
REM -- End of commands from the MISSING dialog
USE 'C:\CCWA\CHAP11\DATA\MISS_EM.syd'
REGRESS
MODEL SALARY =  TIME + PUB+ SEX+ CIT /n = 87
ESTIMATE 

