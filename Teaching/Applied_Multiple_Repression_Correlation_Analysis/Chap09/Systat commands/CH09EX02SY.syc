REM--SYSTAT CODE FILE C09E02SY.SYC READS DATA FILE C0902DT.TXT
REM--CHAPTER 9 EXAMPLE 2 OF 4 HOSPITALS AND THREE TREATMENS CODED BY ALTERNATIVE METHODS

IMPORT "c:\ccwa\CHAP09\DATA\C0902DT.txt" / TYPE=ASCII

REM--CODING THE 4 HOSPITALS WITH 3 EFFECTS CODES WHERE HOSPITAL 4 IS THE OMITTED HOSPITAL
REM--THESE CODES MATCH TABLE 9.1.5
IF HOSPITAL = 1 THEN LET HOSP1E = 1
IF (HOSPITAL = 2) OR (HOSPITAL = 3) THEN LET HOSP1E = 0
IF HOSPITAL = 4 THEN LET HOSP1E = -1
IF HOSPITAL = 2 THEN LET HOSP2E = 1
IF (HOSPITAL = 1) OR (HOSPITAL = 3) THEN LET HOSP2E = 0
IF HOSPITAL = 4 THEN LET HOSP2E = -1
IF HOSPITAL = 3 THEN LET HOSP3E = 1
IF (HOSPITAL = 1) OR (HOSPITAL = 2) THEN LET HOSP3E = 0
IF HOSPITAL = 4 THEN LET HOSP3E = -1
REM--CODING THE THREE TREATMENS BY DUMMY CODES WITH CONTROL TREATMEN AS THE REFERENCE
LET TREAT4D = 0
IF TREATMEN = 1 THEN LET TREAT4D = 1
LET TREAT5D = 0
IF TREATMEN = 2 THEN LET TREAT5D = 1
REM-- CODING THE SIX HOSPITAL BY TREATMEN INTERACTIONS AS PRODUCTS OF THE FIRST 5 HOSPITAL AND TREATMEN VARIABLES
LET HbyTD6 = HOSP1E*TREAT4D
LET HbyTD7 = HOSP1E*TREAT5D
LET HbyTD8 = HOSP2E*TREAT4D
LET HbyTD9 = HOSP2E*TREAT5D
LET HbyTD10 = HOSP3E*TREAT4D
LET HbyTD11 = HOSP3E*TREAT5D
REM-- CODING THE TREATMENS AS EFFECTS CODES WITH THE SECOND TREATMEN (#3) AS THE "OMITTED" CATEGORY
LET TREAT4E = 0
IF TREATMEN = 2 THEN LET TREAT4E = 1
IF TREATMEN = 3 THEN LET TREAT4E = -1
IF TREATMEN = 1 THEN LET TREAT5E = 1
IF TREATMEN = 2 THEN LET TREAT5E = 0
IF TREATMEN = 3  THEN LET TREAT5E = -1
REM-- CODING THE INTERACTIONS USING THE TREATMEN EFFECTS CODES
LET HbyTE6 = HOSP1E*TREAT4E
LET HbyTE7 = HOSP1E*TREAT5E
LET HbyTE8 = HOSP2E*TREAT4E
LET HbyTE9 = HOSP2E*TREAT5E
LET HbyTE10 = HOSP3E*TREAT4E
LET HbyTE11 = HOSP3E*TREAT5E

RUN
ANOVA
CATEGORY HOSPITAL TREATMEN
COVAR 
DEPEND Y
ESTIMATE
REM-- THE FOLLOWING ANALYSES ARE THE EQUATIONS NECESSARY TO COMPUTE THE TYPE III EFFECTS WITH UNEQUAL n IN CELLS
REM -- IN THESE ANALYSES WE MUST USE EFFECTS CODES FOR THE TREATMEN VARIABLES
REGRESS
MODEL Y = CONSTANT + HOSP1E + HOSP2E + HOSP3E  + TREAT4E + TREAT5E + HbyTE6 +HbyTE7+ HbyTE8+ HbyTE9 + HbyTE10 + HbyTE11
ES
REM -- THE NEXT ANALYSES ESTIMATE THE COMBINED TREATMEN AND INTERACTION EFFECTS
REGRESS
MODEL Y = CONSTANT +  TREAT4E + TREAT5E + HbyTE6 +HbyTE7+ HbyTE8+ HbyTE9 + HbyTE10 + HbyTE11
ES
REM -- THE NEXT ANALYSES EXAMINE THE HOSPITAL AND INTERACTIONS
REGRESS
MODEL Y = CONSTANT + HOSP1E + HOSP2E + HOSP3E  + HbyTE6 +HbyTE7+ HbyTE8+ HbyTE9 + HbyTE10 + HbyTE11
ES
REM-- THE ANOVA ESTIMATES ARE DETERMINED BY THE SUBTRACTION METHOD DESCRIBED ON PAGE 362, SEE tABLE 9.1.4.
REM-- THE REGRESSION ESTIMATES ON PAGE 366 ARE PRODUCED IN THE FOLLOWING EQUATION.
REGRESS
MODEL Y = CONSTANT + HOSP1E + HOSP2E + HOSP3E  + TREAT4D + TREAT5D + HbyTD6 +HbyTD7+ HbyTD8+ HbyTD9,
+ HbyTD10 + HbyTD11
ES
