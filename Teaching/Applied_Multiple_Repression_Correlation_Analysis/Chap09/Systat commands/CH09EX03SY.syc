REM--SYSTAT CODE FILE C09E03SY.SYC READS DATA FILE C0903DT.TXT
REM--CHAPTER 9 EXAMPLE 3 testing Dweck's theory with 3 levels of attribution,2 levels of difficulty,
and 2 levels of FAILURE 

IMPORT "c:\ccwa\CHAP09\DATA\C0903DT.txt" / TYPE=ASCII
BY ATTRIB DIFF FAIL
STATS
STATS Y /  Maximum Mean Minimum SD N
BY
basic
REM-CODING AS DUMMY VARIABLES
let A1_D = 0
if attrib = 1 then let A1_D = 1
let A2_D = 0
if attrib = 3 then let A2_D = 1
let  D3_D = 0
IF DIFF = 1 THEN LET D3_D = 1
LET F4_D = 0
IF FAIL = 1 THEN LET F4_D = 1
REM-CODING TWO WAY INTERACTIONS BETWEEN DUMMY VARIABLES
LET A1D3_D = A1_D*D3_D
LET A2D3_D = A2_D*D3_D
LET A1F4_D = A1_D*F4_D
LET A2F4_D = A2_D*F4_D
LET D3F4_D = D3_D*F4_D
REM--CODING THREE WAY INTERACTIONS BETWEEN DUMMY VARIABLES
LET A1D3F4_D = A1_D*D3_D*F4_D
LET A2D3F4_D = A2_D*D3_D*F4_D
RUN
REGRESS
MODEL Y = CONSTANT+A1_D+A2_D+D3_D+F4_D
ESTIMATE
MODEL Y = CONSTANT+A1_D+A2_D+D3_D+F4_D+A1D3_D+A2D3_D+A1F4_D+A2F4_D+D3F4_D
ESTIMATE
MODEL Y = CONSTANT+A1_D+A2_D+D3_D+F4_D+A1D3_D+A2D3_D+A1F4_D+A2F4_D+D3F4_D+A1D3F4_D+A2D3F4_D
ESTIMATE

REM--ALTERNATIVE METHODS OF CODING STUDY CONDITIONS
LET A1_U =0
IF ATTRIB = 1 THEN LET A1_U = 1
IF ATTRIB = 2 THEN LET A1_U = -1
LET A2_U = 0
IF ATTRIB = 2 THEN LET A2_U = -1
IF ATTRIB = 3 THEN LET A2_U = 1
LET D3_U = 1
IF DIFF = 2 THEN LET D3_U = -1
LET F4_U = 1
IF FAIL = 2 THEN LET F4_U = -1
LET A1D3_U = A1_U*D3_U
LET A2D3_U = A2_U*D3_U
LET A1F4_U = A1_U*F4_U
LET A2F4_U = A2_U*F4_U
LET D3F4_U = D3_U*F4_U
LET A1D3F4_U = A1_U*D3_U*F4_U
LET A2D3F4_U = A2_U*D3_U*F4_U
RUN
REGRESS
MODEL Y = CONSTANT+A1_U+A2_U+D3_U+F4_U+A1D3_U+A2D3_U+A1F4_U+A2F4_U+D3F4_U+A1D3F4_U+A2D3F4_U
ESTIMATE
REM--THIS ANALYSIS IS EQUIVALENT TO THE ANOVA ANALYSIS

REM--CODING BY ALTERNATIVE METHODS OMITTING THE TRIPLE INTERACTIONS THAT DID NOT CONTRIBUTE
BASIC
LET A1_W = 0
IF ATTRIB = 1 THEN LET A1_W = 1
IF ATTRIB = 2 THEN LET A1_W = -(37/46) 
LET A2_W = 0
IF ATTRIB = 3 THEN LET A2_W = 1
IF ATTRIB = 2 THEN LET A2_W = -(37/46)
LET D3_W = 1
IF DIFF = 2 THEN LET D3_W = -(56/64)
LET F4_W = 1
IF FAIL = 2 THEN LET F4_W = -(61/59)
LET A1D3_W = A1_W*D3_W
LET A2D3_W = A2_W*D3_W
LET A1F4_W = A1_W*F4_W
LET A2F4_W = A2_W*F4_W
LET D3F4_W = D3_W*F4_W
LET A1D3F4_W =A1_W*D3_W*F4_W
LET A2D3F4_W=A2_W*D3_W*F4_W
LET A1_C = 0
IF ATTRIB = 1 THEN LET A1_C = .5
IF ATTRIB = 3 THEN LET A1_C = -.5
LET A2_C = -.5
IF ATTRIB = 2 THEN LET A2_C = 1
LET D3_C = .5
IF DIFF = 2 THEN LET D3_C = -.5
LET F4_C = .5
IF FAIL = 2 THEN LET F4_C = -.5
LET A1D3_C = A1_C*D3_C
LET A2D3_C = A2_C*D3_C
LET A1F4_C = A1_C*F4_C
LET A2F4_C = A2_C*F4_C
LET D3F4_C = D3_C*F4_C
RUN
REGRESS
MODEL Y = CONSTANT+A1_W+A2_W+D3_W+F4_W + A1D3_W + A2D3_W + A1F4_W + A2F4_W + D3F4_W + A1D3F4_W + A2D3F4_W
ESTIMATE
REM-- NOTE THAT ALTHOUGH THE MAIN EFFECT AND 2WAY INTERACTION COEFFICIENTS ARE DIFFERENT,
 FROM THE PREVIOUS CODES THE HIGHEST ORDER INTERACTIONS WOULD BE THE SAME
MODEL Y = CONSTANT+A1_C+A2_C+D3_C+F4_C  + A1D3_C + A2D3_C + A1F4_C + A2F4_C + D3F4_C
ESTIMATE
