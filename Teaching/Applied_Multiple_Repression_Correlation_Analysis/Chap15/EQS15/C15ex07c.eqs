/TITLE
 C15EX07C -- YA MODEL 02
 (ADDS RANDOM SLOPE & COVARIANCE BETWEEN RANDOM INTERCEPT & SLOPE)
/SPECIFICATIONS
 DATA='C:\EQS\EQS15\C1507DT.ESS'; VARIABLES= 5; CASES=   60;
 METHODS=ML;  MATRIX=RAW; ANALYSIS=MOMENT;
/LABELS
 V1=YA1; V2=YA2; V3=YA3; V4=YA4; V5=YA5;
 F10 = CNST; F11 = LINE;  
/EQUATIONS
 V1  = F10   + 0F11 + E1; ! (NOTE ADDITIONAL SPECIFICATIONS FOR SLOPE HERE)
 V2  = F10   + 1F11 + E2;
 V3  = F10   + 2F11 + E3;
 V4  = F10   + 3F11 + E4;
 V5  = F10   + 4F11 + E5;
!
 F10 =  5*V999       + D10;
 F11 =   0V999       + D11;
/VARIANCES
 D10 = *; D11 = *; ! (ADDS RANDOM SLOPE HERE)
 E1 TO E5 = .2*;
/COVARIANCES
 D10,D11 = *;      !  (ADDS COVARIANCE BETWEEN RANDOM INTERCEPT & SLOPE HERE)
/CONSTRAINTS
 (E1,E1)=(E2,E2)=(E3,E3)=(E4,E4)=(E5,E5);  
/TECHNICAL
 ITERATIONS = 500;
/PRINT 
 FIT=ALL;
/LMTEST
/END

